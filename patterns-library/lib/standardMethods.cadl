import "@cadl-lang/rest";

using Cadl.Http;

namespace ApiPatterns;

// REFERENCE: Chapter 2, Listing 2.6
@error
@doc("Error response")
model OperationError {
  @doc("A server-defined code that uniquely identifies the error.")
  code: string;
  @doc("A human-readable representation of the error.")
  message: string;
  @doc("Additional details about this error.")
  details?: unknown;
}

// *** Standard Operations ***
/*
The operations below are based on the API design patterns described in chapter 7 of the API Design Pattners book.
These represent an interpretation--and are intended to be a starting point for discussion.
If you have thoughts on how to improve these, please start a conversation in the PatternsLibrary repo.
https://github.com/APIPatterns/PatternsLibrary/discussions
*/

model Patch<T> {
  @header contentType: "application/merge-patch+json";
  ...T;
}

@post
op CreateResource<T>(resource: T): T | OperationError;

@get
op GetResource<T>(@path id: string): T | OperationError;

@get
op ListResources<T>(@query filter: string): T | OperationError;

@patch
op UpdateResource<T>(@path id: string, resource: Patch<T>): T | OperationError;

@put
op ReplaceResource<T>(@path id: string, resource: Patch<T>): T | OperationError;

@delete
@doc("Delete a {.name}")
op DeleteResource<T>(@path id: string): T | OperationError;

//TODO: Determine the proper way to pass parameters into the @doc decorators. 
interface ResourceOperations<T> {
  @doc("Create a resource. An important note about this operation is that the resourse ID is generated by the server.")
  create is CreateResource<T>;

  @doc("Get a resource")
  get is GetResource<T>;

  @doc("List resources")
  list is ListResources<T>;

  @doc("Update a resource. This operation supports the partial updating of a resource.")
  update is UpdateResource<T>;

  @doc("Replace a resource. This is a FULL replace.")
  replace is ReplaceResource<T>;
  
  @doc("Delete a resource. This is an immediate delete, e.g. not a soft delete.")
  delete is DeleteResource<T>;
}

// *** END: Standard Operations ***